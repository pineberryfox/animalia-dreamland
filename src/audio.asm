.include "constants.inc"
.include "notes.inc"

.import melodies, bass

.segment "CODE"

.export init_apu
.proc init_apu
	LDY #$13
reset:	LDA apu_initial_state,Y
	STA $4000,Y
	DEY
	BPL reset

	;; skip $4014 (OAMDMA)
	LDA #$0F
	STA $4015
	LDA #$40
	STA $4017
	RTS
.endproc


.export advance_audio
.proc advance_audio
	PHA
	TXA
	PHA
	TYA
	PHA
	LDA #$00
	STA base
	STA channel
	LDA #$40
	STA base + 1
	LDA song
	STA cursong
	LDA song + 1
	STA cursong + 1
	LDA duration
	STA curduration
	JSR in_advance_audio
	LDA curduration
	STA duration
	LDA cursong
	STA song
	LDA cursong + 1
	STA song + 1

	INC channel
	LDA #$08
	STA base
	LDA song + 2
	STA cursong
	LDA song + 3
	STA cursong + 1
	LDA duration + 1
	STA curduration
	JSR in_advance_audio
	LDA curduration
	STA duration + 1
	LDA cursong
	STA song + 2
	LDA cursong + 1
	STA song + 3

	INC channel
	LDA #$04
	STA base
	LDA sfx
	STA cursong
	LDA sfx + 1
	STA cursong + 1
	LDA duration + 2
	STA curduration
	JSR in_advance_audio
	LDA curduration
	STA duration + 2
	LDA cursong
	STA sfx
	LDA cursong + 1
	STA sfx + 1

	PLA
	TAY
	PLA
	TAX
	PLA
	RTS
.endproc
.proc in_advance_audio
	DEC curduration
	BEQ continue
	RTS
continue:
	LDY #$00
	LDA (cursong),Y
	BMI commands
	TAX
	INY
	LDA (cursong),Y
	STA curduration
	STX temp
	LDX channel
	LDA shape,X
	ORA volume_flags,X
	LDX temp
	CPX #$7F
	BNE playnote
	LDA #$90
	LDX #$00
playnote:
	LDY #$00
	STA (base),Y
	LDA period_table_hi,X
	INY
	INY
	INY
	STA (base),Y
	LDA period_table_lo,X
	DEY
	STA (base),Y

	LDA cursong
	CLC
	ADC #$02
	STA cursong
	LDA cursong + 1
	ADC #$00
	STA cursong + 1
	RTS
commands:
	EOR #$FF
	ASL A
	TAX
	LDA jmptable,X
	STA command
	LDA jmptable+1,X
	STA command+1
	LDX channel
	JMP (command) ; tail-call
.endproc


	;; song number in A register
.export load_song
.proc load_song
	CMP #$FF
	BNE real
	LDA #<silence
	STA song
	STA song+2
	LDA #>silence
	STA song + 1
	STA song + 3
	JMP end
real:	ASL A
	TAX
	LDA melodies,X
	STA song
	LDA melodies+1,X
	STA song+1
	LDA bass,X
	STA song+2
	LDA bass+1,X
	STA song+3
end:	LDA #$01
	STA duration
	STA duration+1
	JMP init_apu ; tail-call
.endproc

.export load_sfx
.proc load_sfx
	LDX #$FF
	STX sfx_playing
	CMP #$FF
	BNE real
	LDX #$00
	STX sfx_playing
	LDA #<silence
	STA sfx
	LDA #>silence
	STA sfx + 1
	JMP end
real:	ASL A
	TAX
	LDA sfx_list,X
	STA sfx
	LDA sfx_list+1,X
	STA sfx+1
end:	LDA #$01
	STA duration+2
	RTS
.endproc


.proc cmd_jump
	LDY #$01
	LDA (cursong),Y
	TAX
	INY
	LDA (cursong),Y
	STA cursong + 1
	STX cursong
	JMP in_advance_audio::continue ; tail-call
.endproc

.proc cmd_stop
	LDA #$00
	STA playing,X
	TAY
	LDA #$80
	STA (base),Y
	RTS
.endproc
.proc cmd_shape
	LDY #$01
	LDA (cursong),Y
	STA shape,X
	LDA cursong
	CLC
	ADC #$02
	STA cursong
	LDA cursong + 1
	ADC #$00
	STA cursong + 1
	JMP in_advance_audio::continue
.endproc
.proc cmd_sustain_on
	LDA volume_flags,X
	ORA #$20
	STA volume_flags,X
	JMP finish_cmd ; tail-call
.endproc
.proc cmd_sustain_off
	LDA volume_flags,X
	AND #$DF
	STA volume_flags,X
	JMP finish_cmd ; tail-call
.endproc
.proc cmd_decay_on
	LDA volume_flags,X
	AND #$7F
	STA volume_flags,X
	JMP finish_cmd ; tail-call
.endproc
.proc cmd_decay_off
	LDA volume_flags,X
	ORA #$10
	STA volume_flags,X
	JMP finish_cmd ; tail-call
.endproc
.proc finish_cmd
	INC cursong
	BNE end
	INC cursong + 1
end:	JMP in_advance_audio::continue ; tail-call
.endproc


.segment "RODATA"
apu_initial_state:
	.byte $30, $08, $00, $00
	.byte $30, $08, $00, $00
	.byte $80, $00, $00, $00
	.byte $30, $00, $00, $00
	.byte $00, $00, $00, $00

jmptable:
	.word cmd_stop, cmd_jump
	.word cmd_sustain_on, cmd_sustain_off
	.word cmd_decay_on, cmd_decay_off
	.word cmd_shape


; NTSC period table generated by mktables.py
.segment "RODATA"
period_table_lo:
	.byte $f1, $7f, $13, $ad, $4d, $f3, $9d, $4c, $00, $b8, $74, $34
	.byte $f8, $bf, $89, $56, $26, $f9, $ce, $a6, $80, $5c, $3a, $1a
	.byte $fb, $df, $c4, $ab, $93, $7c, $67, $52, $3f, $2d, $1c, $0c
	.byte $fd, $ef, $e1, $d5, $c9, $bd, $b3, $a9, $9f, $96, $8e, $86
	.byte $7e, $77, $70, $6a, $64, $5e, $59, $54, $4f, $4b, $46, $42
	.byte $3f, $3b, $38, $34, $31, $2f, $2c, $29, $27, $25, $23, $21
	.byte $1f, $1d, $1b, $1a, $18, $17, $15, $14
period_table_hi:
	.byte $07, $07, $07, $06, $06, $05, $05, $05, $05, $04, $04, $04
	.byte $03, $03, $03, $03, $03, $02, $02, $02, $02, $02, $02, $02
	.byte $01, $01, $01, $01, $01, $01, $01, $01, $01, $01, $01, $01
	.byte $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00
	.byte $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00
	.byte $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00
	.byte $00, $00, $00, $00, $00, $00, $00, $00


.segment "ZEROPAGE"
.exportzp duration,song,sfx,sfx_playing
command: .res 2
channel: .res 1
duration: .res 3
song: .res 4
sfx:  .res 2
volume_flags: .res 3
shape: .res 3
playing: .res 2      ;\___ these are one unit
sfx_playing: .res 1  ;/

.segment "ZEROPAGE"
cursong: .res 2
curduration: .res 1
temp: .res 1
base: .res 2

.segment "RODATA"

JUMP_BASE = 2*12+1
sfx_jump:
	.byte CMD_DECAY_OFF, CMD_SUSTAIN_OFF
	.byte CMD_SHAPE, $88
	.byte JUMP_BASE, 1
	.byte JUMP_BASE+1, 1
	.byte JUMP_BASE+2, 1
	.byte JUMP_BASE+4, 1
	.byte JUMP_BASE+6, 1
	.byte JUMP_BASE+9, 1
	.byte JUMP_BASE+12, 1
	.byte CMD_STOP

sfx_pickup:
	.byte CMD_SHAPE, $02
	.byte CMD_DECAY_OFF, CMD_SUSTAIN_ON
	.byte 2*12+7, 2
	.byte CMD_SHAPE, $4F
	.byte 2*12+8, 1
	.byte CMD_SHAPE, $8F
	.byte 2*12+9, 2
	.byte CMD_SHAPE, $CF
	.byte 2*12+10, 1
	.byte CMD_SHAPE, $0F
	.byte 3*12, 1
	.byte CMD_SHAPE, $4F
	.byte 3*12+10, 1
	.byte CMD_SHAPE, $8F
	.byte 3*12+6, 1
	.byte CMD_SHAPE, $CF
	.byte 4*12+3, 1
.export silence
silence: .byte CMD_STOP

sfx_shwp:
	.byte CMD_SHAPE, $0F
	.byte CMD_DECAY_OFF
	.byte 5, 1
	.byte 7, 1
	.byte 6, 1
	.byte 8, 1
	.byte 5, 1
	.byte CMD_STOP



.segment "RODATA"
sfx_list:
	.word silence, sfx_jump, sfx_pickup, sfx_shwp
